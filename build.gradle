buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'nu.studer:gradle-jooq-plugin:2.0.11'
        classpath "com.vanniktech:gradle-dependency-graph-generator-plugin:0.5.0"
    }
}

group 'ru.rem.training.gradle'
version '1.0-SNAPSHOT'

apply plugin: "java"
apply plugin: "com.vanniktech.dependency.graph.generator"
apply plugin: "nu.studer.jooq"

repositories {
    mavenCentral()
    maven {
        url 'https://plugins.gradle.org/m2/'
    }
}

dependencies {
    jooqRuntime 'postgresql:postgresql:9.1-901.jdbc4'
}

subprojects {
    apply plugin: "java"

    tasks.withType(JavaCompile) {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    repositories {
        mavenCentral()
    }
}

jooq {
    version = '3.10.8'
    edition = 'OSS'
    sample(sourceSets.main) {
        jdbc {
            driver   = 'org.postgresql.Driver'
            url      = 'jdbc:postgresql://localhost:5432/threesh'
            user     = 'postgres'
            password = 'postgres'
        }
        generator {
            // Older jOOQ versions had a package name conflict between the jooq and jooq-meta / jooq-codegen artefacts,
            // which all reused the org.jooq.util package. This was OK in older versions of Java, up to Java 8.   
            // https://stackoverflow.com/questions/50793357/java-lang-classnotfoundexception-org-jooq-util-javagenerator
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.postgres.PostgresDatabase'
                inputSchema = 'public'
                forcedTypes {
                    forcedType {
                        name = 'varchar'
                        expression = '.*'
                        types = 'JSONB?'
                    }
                    forcedType {
                        name = 'varchar'
                        expression = '.*'
                        types = 'INET'
                    }
                }
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'ru.rem.db'
                directory = 'db_project/src/generated/java'
            }
        }
    }
}

wrapper {
    gradleVersion = "5.4.1"
}